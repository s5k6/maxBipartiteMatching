#!/usr/bin/gnuplot

dir = "./tmp";

term = "wxt";
term = "png"
#term = "svg"
#term="pdf";



set terminal term

if (term eq "png") set terminal term size 1500, 1000
if (term eq "svg") set terminal term size 1200, 800 background rgb 'white'
if (term eq "pdf") set terminal term size 29.7 cm, 21.0 cm

set key vertical rmargin samplen 1
set autoscale fix

# entry in memory column: did not finish, time will be the limit
set datafile missing "DNF"

styleFgl       = "title 'fgl-maxFlow' with points pt 2 ps 1 lt 1"
styleDanilenko = "title 'Danilenko'   with points pt 2 ps 1 lt 4"
styleMatcher   = "title 'my algo'     with points pt 2 ps 1 lt 2"
styleOther     = "with points pt 2 ps 1 lt 7"

density = "(column(m)/(column(l)*column(r)))"
size = "(column(l) + column(r) + column(m))"
nodes = "(column(l) + column(r))"

#______________________________________________________________________
# plotting the distribution of samples


set title "{/=18 distribution of samples}\nedge count / node count"
set xlabel "#V"
set ylabel "#E"
if (term ne "wxt") set output sprintf("%s/nodes_edges.%s", dir, term)

# format: <graphId> <left> <right> <edges>

plot "tmp/nodes_edges.data"  using ($2 + $3):4  title 'sample' @styleOther

if (term eq "wxt") pause mouse close



set title "{/=18 distribution of samples}\ndensity / size"
set xlabel "#V + #E"
set ylabel "#E / (#left * #right)"
if (term ne "wxt") set output sprintf("%s/density_size.%s", dir, term)

# format: <graphId> <left> <right> <edges>

plot "tmp/nodes_edges.data"  using ($2 + $3 + $4):($4 /($2*$3))  title 'sample' @styleOther

if (term eq "wxt") pause mouse close


#______________________________________________________________________
# plotting the timings 


# layout of data files
# id   l    r    m    [s]    [kB]
  s=1; l=2; r=3; m=4; cpu=5; rss=6;



set title "{/=18 CPU}\nrunning time / graph size"
set xlabel "#V + #E"
set ylabel "time [s]"
if (term ne "wxt") set output sprintf("%s/cpu_nodes+edges.%s", dir, term)

plot "tmp/timings.fgl"        using @size:cpu  @styleFgl, \
     "tmp/timings.danilenko"  using @size:cpu  @styleDanilenko, \
     "tmp/timings.matcher"    using @size:cpu  @styleMatcher

if (term eq "wxt") pause mouse close



set title "{/=18 CPU}\nrunning time / node count"
set xlabel "#V"
set ylabel "time [s]"
if (term ne "wxt") set output sprintf("%s/cpu_nodes.%s", dir, term)

plot "tmp/timings.fgl"        using @nodes:cpu  @styleFgl, \
     "tmp/timings.danilenko"  using @nodes:cpu  @styleDanilenko, \
     "tmp/timings.matcher"    using @nodes:cpu  @styleMatcher

if (term eq "wxt") pause mouse close



set title "{/=18 CPU}\nrunning time / edge count"
set xlabel "#E"
set ylabel "time [s]"
if (term ne "wxt") set output sprintf("%s/cpu_edges.%s", dir, term)

plot "tmp/timings.fgl"        using m:cpu  @styleFgl, \
     "tmp/timings.danilenko"  using m:cpu  @styleDanilenko, \
     "tmp/timings.matcher"    using m:cpu  @styleMatcher

if (term eq "wxt") pause mouse close



set title "{/=18 CPU}\nrunning time / density"
set xlabel "#E / (#left * #right)"
set ylabel "time [s]"
if (term ne "wxt") set output sprintf("%s/cpu_density.%s", dir, term)

plot "tmp/timings.fgl"        using @density:cpu  @styleFgl, \
     "tmp/timings.danilenko"  using @density:cpu  @styleDanilenko, \
     "tmp/timings.matcher"    using @density:cpu  @styleMatcher

if (term eq "wxt") pause mouse close



set title "{/=18 Memory}\nmemory consumption / graph size"
set xlabel "#V + #E"
set ylabel "max RSS [kB]"
if (term ne "wxt") set output sprintf("%s/mem_nodes+edges.%s", dir, term)

plot "tmp/timings.fgl"        using @size:rss  @styleFgl, \
     "tmp/timings.danilenko"  using @size:rss  @styleDanilenko, \
     "tmp/timings.matcher"    using @size:rss  @styleMatcher

if (term eq "wxt") pause mouse close



set title "{/=18 Memory}\nmemory consumption / node count"
set xlabel "#V"
set ylabel "max RSS [kB]"
if (term ne "wxt") set output sprintf("%s/mem_nodes.%s", dir, term)

plot "tmp/timings.fgl"        using @nodes:rss  @styleFgl, \
     "tmp/timings.danilenko"  using @nodes:rss  @styleDanilenko, \
     "tmp/timings.matcher"    using @nodes:rss  @styleMatcher

if (term eq "wxt") pause mouse close



set title "{/=18 Memory}\nmemory consumption / edge count"
set xlabel "#E"
set ylabel "max RSS [kB]"
if (term ne "wxt") set output sprintf("%s/mem_edges.%s", dir, term)

plot "tmp/timings.fgl"        using m:rss  @styleFgl, \
     "tmp/timings.danilenko"  using m:rss  @styleDanilenko, \
     "tmp/timings.matcher"    using m:rss  @styleMatcher

if (term eq "wxt") pause mouse close



set title "{/=18 Memory}\nmemory consumption / density"
set xlabel "#E / (#left * #right)"
set ylabel "max RSS [kB]"
if (term ne "wxt") set output sprintf("%s/mem_density.%s", dir, term)

plot "tmp/timings.fgl"        using @density:rss  @styleFgl, \
     "tmp/timings.danilenko"  using @density:rss  @styleDanilenko, \
     "tmp/timings.matcher"    using @density:rss  @styleMatcher

if (term eq "wxt") pause mouse close
